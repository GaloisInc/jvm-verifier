// This SAWScript file contains definitions common to many proofs.  It
// also imports the SBV representations of functions from the
// Cryptol reference implementation.

// Define field prime for P384 curve (taken from NIST and NSA documents).
let field_prime = <| 2^384 - 2^128 - 2^96 + 2^32 - 1 |> : [384];

////////////////////////////////////////////////////////////////////////////////
// Imported functions from the Cryptol specification

extern SBV ref_neg("../spec/sbv/generated/ref_p384_neg.sbv")
  : [384] -> [384];

extern SBV ref_is_field("../spec/sbv/generated/ref_p384_is_val.sbv")  
  : [384] -> Bit;

extern SBV ref_field_half("../spec/sbv/generated/ref_p384_half.sbv")
  : [384] -> [384];

extern SBV ref_field_mul("../spec/sbv/generated/ref_p384_mul.sbv")
  : ([384],[384]) -> [384];

// extern SBV ref_prime_div("../spec/sbv/generated/ref_p384_prime_div.sbv")
//  : ([384],[384],[384]) -> [384];

extern SBV ref_field_mod("../spec/sbv/generated/ref_p384_mod.sbv")
  : [768] -> [384];

extern SBV ref_field_sq("../spec/sbv/generated/ref_p384_sq.sbv")
  : [384] -> [384];

extern SBV ref_field_add("../spec/sbv/generated/ref_p384_add.sbv") 
  : ([384],[384]) -> [384];

extern SBV ref_field_sub("../spec/sbv/generated/ref_p384_sub.sbv") 
  : ([384],[384]) -> [384];

extern SBV ref_ec_double("../spec/sbv/generated/ref_p384_ec_double.sbv")
  : { x : [384]; y : [384]; z : [384] } -> { x : [384]; y : [384]; z : [384] };

extern SBV ref_incFieldPrime("../spec/sbv/generated/ref_p384_incFieldPrime.sbv")
  : [384] -> { carry : [32]; rslt : [384] };

extern SBV ref_decFieldPrime("../spec/sbv/generated/ref_p384_decFieldPrime.sbv")
  : [384] -> { carry : [32]; rslt : [384] };

extern SBV ref_ec_full_add("../spec/sbv/generated/ref_p384_ec_full_add.sbv") 
  : ({ x: [384]; y: [384]; z: [384] }, { x:[384]; y:[384] }) -> { x:[384]; y:[384]; z:[384] };

extern SBV ref_ec_full_sub("../spec/sbv/generated/ref_p384_ec_full_sub.sbv") 
  : ({ x: [384]; y: [384]; z: [384] }, { x:[384]; y:[384] }) -> { x:[384]; y:[384]; z:[384] };

////////////////////////////////////////////////////////////////////////////////
// Rewrite Rules
//
// This section defines common rules for verification tasks that use the
// rewriter.
// 
// Strategies for creating rewrite rules:
//   Push split and join towards bottom of terms.
//   Prefer join to split in equals.
//   Cancel unnessary applications wherever possible
//     with cancelation/elimination rules.

// Misc cancelation rules

rule eq_elim    : forAll {x:a}. x == x -> True;
rule join_split : forAll {x:[384]}. join(split(x) : [12][32]) -> x;
rule not_not    : forAll {x:Bit}. not(not(x)) -> x;
rule eq_elim    : forAll {x:a}. x == x -> True;
rule join_split : forAll {x:[384]}. join(split(x) : [12][32]) -> x;

// Boolean elimination rules 

rule and_true_elim1  : forAll {x:Bit}. True && x -> x;
rule and_true_elim2  : forAll {x:Bit}. x && True -> x;
rule and_false_elim1 : forAll {x:Bit}. False && x -> False;
rule and_false_elim2 : forAll {x:Bit}. x && False -> False;
rule and_left_assoc  : forAll {x:Bit, y:Bit, z:Bit}.  x && (y && z) -> (x && y) && z;

rule or_true_elim1  : forAll {x:Bit}. True || x -> True;
rule or_true_elim1  : forAll {x:Bit}. x || True -> True;
rule or_false_elim1 : forAll {x:Bit}. False || x -> x;
rule or_false_elim2 : forAll {x:Bit}. x || False -> x;
rule or_trivial     : forAll {x:Bit}. not x || x -> True;

rule or_redundant1  : forAll {x:Bit,y:Bit}. (x || y) || y -> x || y;
rule or_redundant2  : forAll {x:Bit,y:Bit}. (x || y) || x -> x || y;
rule and_redundant1 : forAll {x:Bit,y:Bit}. (x && y) && y -> x && y;
rule and_redundant2 : forAll {x:Bit,y:Bit}. (x && y) && x -> x && y;

// De Morgan's 

rule not_or  : forAll {x:Bit, y:Bit}. not (x || y) -> not x && not y;
rule not_and : forAll {x:Bit, y:Bit}. not (x && y) -> not x || not y;

// Generic ite rules 

rule ite_not :
  forAll { b:Bit, x:a, y:a }.
    (if (not b) then x else y) -> if b then y else x;

rule ite_elim_1 : forAll { b:Bit, x:a, y:a, z:a }.
  (if b then (if b then x else y) else z)
  -> if b then x else z;

rule ite_elim_2 : forAll { b:Bit, x:a, y:a, z:a }.
  (if b then x else (if b then y else z))
  -> if b then x else z;

rule ite_bit : forAll {b:Bit, c:Bit, d:Bit}.
  if b then c else d -> (not b || c) && (b || d);

// Bit-level ite rules

rule ite_bit_false_1 :
  forAll { b:Bit, c:Bit }.
    (if b then False else c) -> not b && c;

rule ite_bit_true_1 :
  forAll { b:Bit, c:Bit }.
    (if b then True else c) -> b || c;

// Congruence rules for ite 

rule ite_eq_cong_1 :
  forAll { b:Bit, x:a, y:a, z:a }.
    (if b then x else y) == z -> if b then (x == z) else (y == z);

rule ite_eq_cong_2 :
  forAll { b:Bit, x:a, y:a, z:a }.
    z == (if b then x else y) -> if b then (z == x) else (z == y);

rule ite_split_cong :
  forAll { b:Bit, x:[384], y:[384] }.
    split(if b then x else y) : [12][32] -> 
      if b then split(x) : [12][32] else split(y) : [12][32];

rule ite_join_cong :
  forAll { b:Bit, x:[12][32], y:[12][32] }.
    join(if b then x else y) -> 
      if b then join(x) else join(y);

// Eq-split rules

let zeroArray = split(0:[384]) : [12][32];
let unitArray = split(1:[384]) : [12][32];

rule eq_split_cong_1 : forAll {x:[384], y:[12][32]}.
  split(x):[12][32] == y -> x == join(y);

rule eq_split_cong_2 : forAll {x:[12][32], y:[384]}.
  x == split(y):[12][32] -> join(x) == y;

// Special purpose eq-split rules
// N.B. In the long term, it should be able to write more general rules
// than this.

rule eq_split_valueOf_zeroArray : forAll { x:[12][32]}.
  x == zeroArray -> join(x) == 0:[384];

rule eq_split_valueOf_unitArray : forAll { x:[12][32]}.
  x == unitArray -> join(x) == 1:[384];

// Record elimination rules

rule jacobianX :
  forAll { x0 : a, y0 : a, z0 : a }.
    { x = x0 ; y = y0 ; z = z0 }.x -> x0;

rule jacobianY :
  forAll { x0 : a, y0 : a, z0 : a }.
    { x = x0 ; y = y0 ; z = z0 }.y -> y0;

rule jacobianZ :
  forAll { x0 : a, y0 : a, z0 : a }.
    { x = x0 ; y = y0 ; z = z0 }.z -> z0;

rule affineX :
  forAll { x0 : a, y0 : a }.
    { x = x0 ; y = y0 }.x -> x0;

rule affineY :
  forAll { x0 : a, y0 : a }.
    { x = x0 ; y = y0 }.y -> y0;

// Field-specific elimination rules

rule field_sq_def :
  forAll { x:[384] }.
   ref_field_sq(x) -> ref_field_mul(x, x);

rule ite_zero_field_mul :
  forAll { x:[384], y:[384], z:[384], c:Bit }.
    (if (x == 0:[384]) then (ref_field_mul(x, y) == z:[384]) else c)
     -> (if (x == 0:[384]) then (z:[384] == 0:[384]) else c);
